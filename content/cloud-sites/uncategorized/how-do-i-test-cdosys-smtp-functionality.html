---
node_id: 649
title: How do I test CDOSYS SMTP functionality?
permalink: article/how-do-i-test-cdosys-smtp-functionality
type: article
created_date: '2011-03-16 21:57:40'
created_by: RackKCAdmin
last_modified_date: '2015-06-25 15:5210'
last_modified_by: kelly.holcomb
products: Cloud Sites
categories: Uncategorized
body_format: tinymce
---

<p>You can use the following code saved into a file with a <em>.asp</em> extension to test CDOSYS functionality; CDOSYS is a built-in component in ASP, so utilizing it from your ASP code takes no additional effort on your part.</p><p>We strongly recommend using an SMTP relay that requires authentication. &nbsp;Sending mail through unauthenticated SMTP servers (including the localhost relay on Cloud Sites) can result in delays or undelivered email due to stringent anti-spam filters.</p><p><a id="Example_using_CDOSYS" name="Example_using_CDOSYS"></a></p><h2><span class="mw-headline">Example using CDOSYS</span></h2><p>You will need to change the following variables:</p><ul><li>.Item(cdoSMTPServer)</li><li>.Item(cdoSendUserName)</li><li>.Item(cdoSendPassword)</li><li>.To</li><li>.From</li></ul><pre>&lt;%
Const cdoSendUsingMethod       = "http://schemas.microsoft.com/cdo/configuration/sendusing"
Const cdoSendUsingPort         = 2
Const cdoSMTPServer            = "http://schemas.microsoft.com/cdo/configuration/smtpserver"
Const cdoSMTPServerPort        = "http://schemas.microsoft.com/cdo/configuration/smtpserverport"
Const cdoSMTPConnectionTimeout = "http://schemas.microsoft.com/cdo/configuration/smtpconnectiontimeout"
Const cdoSMTPAuthenticate      = "http://schemas.microsoft.com/cdo/configuration/smtpauthenticate"
Const cdoBasic                 = 1
Const cdoSendUserName          = "http://schemas.microsoft.com/cdo/configuration/sendusername"

Const cdoSendPassword          = "http://schemas.microsoft.com/cdo/configuration/sendpassword"

Dim objConfig  ' As CDO.Configuration
Dim objMessage ' As CDO.Message
Dim Fields     ' As ADODB.Fields

' Get a handle on the config object and it's fields
Set objConfig = Server.CreateObject("CDO.Configuration")
Set Fields = objConfig.Fields

' Set config fields we care about
With Fields
        .Item(cdoSendUsingMethod)       = cdoSendUsingPort
        .Item(cdoSMTPServer)            = "mail.(domain.com)"
        .Item(cdoSMTPServerPort)        = 25
        .Item(cdoSMTPConnectionTimeout) = 10
        .Item(cdoSMTPAuthenticate)      = cdoBasic
        .Item(cdoSendUserName)          = "webmaster@example.com"
        .Item(cdoSendPassword)          = "yourPassword"

        .Update
End With

Set objMessage = Server.CreateObject("CDO.Message")

Set objMessage.Configuration = objConfig

With objMessage
        .To       = "Nobody &lt;nobody@example.com&gt;"
        .From     = "Web Master &lt;webmaster@example.com&gt;"
        .Subject  = "Test message using CDOSYS SMTP"

        .TextBody = "This is a test email message using CDOSYS SMTP Sent @ " &amp; Now()
        .Send
End With

Set Fields = Nothing
Set objMessage = Nothing
Set objConfig = Nothing
%&gt;
</pre><p><a id="See_Also" name="See_Also"></a></p><h2><span class="mw-headline">See Also</span></h2><ul><li><a title="How do I test PHP SMTP functionality?" href="/knowledge_center/index.php/How_do_I_test_PHP_SMTP_functionality%3F">How do I test PHP SMTP functionality?</a></li></ul>
